/* file\LineIO.nrx    

   Line-mode I/O using buffered reader and printer writer.
   Extract 'DEVICE' statements from a file (default CONFIG.SYS).  */

   parse arg filename
   if filename = '' then filename = 'C:/CONFIG.SYS'
   output = 'CONFIG.DEV'
   say 'File:' filename '->' output

   inFile  = FileReader(filename)                   -- input file
   source  = BufferedReader(inFile)                 -- buffered

   outFile = FileWriter(output)                     -- output file
   dest    = PrintWriter(outFile)                   -- to printer

   loop forever
      textline = source.readLine()                  -- read the file    
      if textline = null then leave                 -- end-of-file ?
      parse textline word1 '=' .
      if word1 = "device" then do                   -- DEVICE statement ?
         dest.println(textline)                     -- write output
      end
   end

   source.close()                                   -- close files
   dest.close()
   
   say 'Extracted DEVICE statements:'               -- display results
   source = BufferedReader( FileReader(output) )    -- read output file
   loop until textline = null
       textline = source.readLine()
       if textline \= null then say ' ' textline    -- standard output
   end
-- end LineIO