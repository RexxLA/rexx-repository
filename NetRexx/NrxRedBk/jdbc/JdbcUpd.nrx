/* jdbc\JdbcUpd.nrx

   This NetRexx program demonstrate DB2 update using the JDBC API.
   Usage: Java JdbcUpd [<DB-URL>] [<userprefix>] [U] */

import java.sql.

parse arg url prefix lowup             -- process arguments
if url = '' then
   url = 'jdbc:db2:sample'
else do                                -- check for correct URL
     parse url p1 ':' p2 ':' rest
     if p1 \= 'jdbc' | p2 \= 'db2' | rest = '' then do
        say 'Usage: java JdbcUpd [<DB-URL>] [<userprefix>] [U]'
        exit 8
     end
end
if prefix = '' then prefix = 'userid'
if lowup \= 'U' then lowup = 'L' 

do                                     -- loading DB2 support
  say 'Loading DB2 driver classes...'
  Class.forName('COM.ibm.db2.jdbc.app.DB2Driver').newInstance()
  -- Class.forName('COM.ibm.db2.jdbc.net.DB2Driver').newInstance()
catch e1 = Exception
  say 'The DB2 driver classes could not be found and loaded !'
  say 'Exception (' e1 ') caught : \n' e1.getMessage()
  exit 1
end                                    -- end : loading DB2 support

do                                     -- connecting to DB2 host
  say 'Connecting to:' url
  jdbcCon = Connection DriverManager.getConnection(url, 'userid', 'password')
catch e2 = SQLException
  say 'SQLException(s) caught while connecting !'
  loop while (e2 \= null)
     say 'SQLState:' e2.getSQLState()
     say 'Message: ' e2.getMessage()
     say 'Vendor:  ' e2.getErrorCode()
     say
     e2 = e2.getNextException()
   end
   exit 1
end                                    -- end : connecting to DB2 host

do                                     -- retrieve employee, update firstname

  say 'Preparing update...'                           -- prepare UPDATE
  updateQ = 'UPDATE' prefix'.EMPLOYEE SET firstnme = ? WHERE empno = ?'
  updateStmt = PreparedStatement jdbcCon.prepareStatement(updateQ)
  say 'Creating query...'                             -- create SELECT
  query = 'SELECT firstnme, lastname, empno FROM' prefix'.EMPLOYEE'
  stmt = Statement jdbcCon.createStatement()
  rs = ResultSet stmt.executeQuery(query)             -- execute select

  loop row=0 while rs.next()                          -- loop employees
    firstname = String rs.getString('firstnme')
    if lowup = 'U' then firstname = firstname.toUpperCase()
    else do
       dChar = firstname.charAt(0)
       firstname = dChar || firstname.substring(1).toLowerCase()
    end
    updateStmt.setString(1, firstname)                -- parms for update
    updateStmt.setString(2, rs.getString('empno'))
    say 'Updating' rs.getString('lastname') firstname ': \0'
    say updateStmt.executeUpdate() 'row(s) updated'   -- execute update
  end

  rs.close()                           -- close the ResultSet
  stmt.close()                         -- close the Statement
  updateStmt.close()                   -- close the PreparedStatement
  jdbcCon.close()                      -- close the Connection
  say 'Updated' row 'employees.'
catch e3 = SQLException
  say 'SQLException(s) caught !'
  loop while (e3 \= null)
     say 'SQLState:' e3.getSQLState()
     say 'Message: ' e3.getMessage()
     say 'Vendor:  ' e3.getErrorCode()
     say
     e3 = e3.getNextException()
   end
end                                    -- end: empoyees