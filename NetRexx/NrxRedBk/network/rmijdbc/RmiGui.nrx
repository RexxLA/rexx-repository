/* network\rmijdbc\RmiGui.nrx

   RMI - JDBC Applet:  GUI Implementation */

package network.rmijdbc

import java.rmi.

import Redbook.

class RmiGui public extends Applet uses GridBagConstraints
      
  Properties constant
    UPDATE   = int 0
    ADD      = int 1
    DELETE   = int 2
    RETRIEVE = int 3

  Properties static
    ctr           = RmiContI               -- RMI controller
    conthost      = Rexx ''                -- host name of controller

  Properties inheritable
    tf_depno      = TextField(3)
    tf_depname    = TextField(29)
    tf_manager    = TextField(6)
    tf_mgrDept    = TextField(3)
    l_departments = List(10)
    ta_message    = TextArea(2,30)
    b1            = Button
    b2            = Button
    b3            = Button
    b4            = Button
    b5            = Button

/*-------------------------- methods --------------------------------*/
  method init() 
    do
      win = this                            -- applet frame
      showStatus('RMI JDBC GUI')            -- status message
      gbl = SimpleGridbagLayout(win)        -- use gridbag layout
      -- ws = WindowSupport(win)               -- close window support

      buildLayout(gbl)                      -- ,ws)

      -- win.pack()
      -- RedbookUtil.positionWindow(win)
      -- win.setVisible(1)
      repaint()

      setMessage('Registering the Security Manager ...')
      do
         System.setSecurityManager(RMISecurityManager())
       catch SecurityException
      end

      conthost = getCodeBase().getHost()    -- controller host
      setMessage('Finding the RMI Controller on:' conthost)
      ctr = RmiContI Naming.lookup('rmi://'conthost'/Controller')
      setMessage('RMI is ready...')

      b1.addActionListener(DoAction(this,ctr,UPDATE))
      b2.addActionListener(DoAction(this,ctr,ADD))
      b3.addActionListener(DoAction(this,ctr,DELETE))
      b4.addActionListener(DoAction(this,ctr,RETRIEVE))
      l_departments.addItemListener(DoAction(this,ctr))

      setMessage('Connecting to database...')
      msg = ctr.connect()
      setMessage(msg)

      setMessage('Retrieving departments...')  
      setDepartmentsList(ctr.retrieveListDep())
    catch e1=RemoteException
      say 'RMI RemoteException caught in init: \n' e1.getMessage()  
    catch e=Exception
      setMessage('Exception (' e ') caught: \n' e.getMessage())
    end

  method buildLayout(gbl = SimpleGridbagLayout)  -- , ws=WindowSupport)
    gbl.addFixSize(Label('Number'),  0,0,Insets(10,10,5,30))
    gbl.addFixSize(Label('Name'),    0,1,Insets( 5,10,5,30))
    gbl.addFixSize(Label('Manager'), 0,2,Insets( 5,10,5,30))
    gbl.addFixSize(Label('Mgr Dept'),0,3,Insets( 5,10,5,30))

    gbl.addVarSize(tf_depno  ,1,0,Insets(10,0,5,5),1.0,0.0)
    gbl.addVarSize(tf_depname,1,1,Insets( 0,0,5,5),1.0,0.0)
    gbl.addVarSize(tf_manager,1,2,Insets( 0,0,5,5),1.0,0.0)
    gbl.addVarSize(tf_mgrDept,1,3,Insets( 0,0,5,5),1.0,0.0)
    tf_depno.addTextListener(LimitTextField(tf_depno,3))
    tf_depname.addTextListener(LimitTextField(tf_depname,29))
    tf_manager.addTextListener(LimitTextField(tf_manager,6))
    tf_mgrDept.addTextListener(LimitTextField(tf_mgrDept,3))

    gbl.addVarSize(l_departments,0,4,Insets(10,10,15,5),1.0,2.0,2)
    gbl.addVarSize(ta_message,   0,5,Insets( 0,10,10,5),1.0,1.0,2)

    b1=Button gbl.addFixSize(Button('Update')  ,2,0,Insets(10,0, 5,10),1,1,HORIZONTAL)
    b2=Button gbl.addFixSize(Button('Add')     ,2,1,Insets( 0,0, 5,10),1,1,HORIZONTAL)
    b3=Button gbl.addFixSize(Button('Delete')  ,2,2,Insets( 0,0, 5,10),1,1,HORIZONTAL)
    b4=Button gbl.addFixSize(Button('Retrieve'),2,3,Insets( 0,0, 5,10),1,1,HORIZONTAL)
    -- b5=Button gbl.addFixSize(Button('Exit')    ,2,5,Insets( 0,0,10,10),1,1,HORIZONTAL,SOUTHWEST)
    -- b5.addActionListener(ws.getCloseWindow())

    cb = ControlButtons()
    cb.addButton(b1)
    cb.addButton(b2)
    cb.addButton(b3)
    tf_depno.addTextListener(cb)
    cb.disable

  method setDepartmentsList(aList = String[])
    l_departments.removeAll()
    loop i = 1 for aList.length - 1
      l_departments.add(aList[i])
    end
    setMessage(aList[0])

  method setDepno(s=String)
    tf_depno.setText(s)

  method setName(s=String)
    tf_depname.setText(s)

  method setMgNo(s=String)
    tf_manager.setText(s)

  method setMgDepNo(s=String)
    tf_mgrDept.setText(s)

  method getDepno() returns String
    return tf_depno.getText()

  method getName() returns String
    return tf_depname.getText()

  method getMgNo() returns String
    if tf_manager.getText() = '' then return null
                                 else return tf_manager.getText()

  method getMgDepNo() returns String
    return tf_mgrDept.getText()

  /*----------------------------- interface to display messages -----------------*/
  method setMessage(msg = String) 
    say msg
    ta_message.append('\n'msg)

/********************************** class DoAction ****************************/

class DoAction implements ActionListener, ItemListener uses RmiGui
  Properties inheritable
    ui  = RmiGui      -- reference to user interface
    ctr = RmiContI    -- reference to the controller
    act = int         -- which action

  method DoAction(aGui=RmiGui,aControl=RmiContI,what=int 0)
    ui  = aGui
    ctr = aControl
    act = what

  method actionPerformed(e=ActionEvent)
    select
      when act = UPDATE then do
        ui.setMessage('Updating a row in the DEPARTMENT table...')
        msg = ctr.update(ui.getDepno(),ui.getName(),ui.getMgNo(),ui.getMgDepNo())
        ui.setMessage(msg)
        ui.setDepartmentsList(ctr.retrieveListDep())
      end
      when act = ADD then do
        ui.setMessage('Inserting a row in the DEPARTMENT table...')
        msg = ctr.add(ui.getDepno(),ui.getName(),ui.getMgNo(),ui.getMgDepNo())
        ui.setMessage(msg)
        ui.setDepartmentsList(ctr.retrieveListDep())
      end
      when act = DELETE then do
        ui.setMessage('Deleting a row in the DEPARTMENT table...')
        msg = ctr.delete(ui.getDepno())
        ui.setMessage(msg)
        ui.setDepartmentsList(ctr.retrieveListDep())
      end
      when act = RETRIEVE then do
        ui.setDepartmentsList(ctr.retrieveListDep())
        ui.setDepno('')     -- clear the fields
        ui.setName('')
        ui.setMgNo('')
        ui.setMgDepNo('')
      end
    catch e1=RemoteException
      say 'RMI RemoteException caught in action' act': \n' e1.getMessage()  
    end

  method itemStateChanged(e=ItemEvent) 
    s = (List e.getItemSelectable()).getSelectedItem()
    if s \= null then
      do
        parse s depno '-' .
        msg = ctr.select(String depno.strip())

        ui.setMessage(msg)
        ui.setDepno(depno)             -- set the fields
        ui.setName(ctr.getDepName())
        ui.setMgNo(ctr.getMgNo())
        ui.setMgDepNo(ctr.getMgDepNo())
      catch e1=RemoteException
        say 'RMI RemoteException caught in itemChanged: \n' e1.getMessage()  
      end

/********************************** class ControlButtons **********************/

class ControlButtons implements TextListener
 Properties inheritable
   buttons = Vector()
   enabled = boolean 1

 method addButton(aButton = Button)
   buttons.addElement(aButton)

 method textValueChanged(e = TextEvent)
   field = TextField e.getSource()
   text = field.getText()
   if text \= null then
     if text = '' then disable()
                  else enable()
   else disable()

 method enable()
   if enabled then return
   enabled = 1
   change()

 method disable()
   if \enabled then return
   enabled = 0
   change()

 method change()
   loop i=0 for buttons.size
     (Button buttons.elementAt(i)).setEnabled(enabled)
   end

-- end RmiGui